dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

AC_INIT([cppcms], [0.0.1], [artyomtnk@yahoo.com])
AC_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE([1.9 foreign])

AC_PROG_CXX
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

AC_LANG_CPLUSPLUS
AC_CONFIG_FILES([Makefile])

AC_ARG_ENABLE(forkcache,[AS_HELP_STRING([--disable-forkcache],[Disable shared memory cache])])
AC_ARG_ENABLE(fastcgi,[AS_HELP_STRING([--disable-fastcgi],[Disable fastcgi interface])])
AC_ARG_ENABLE(tcpcache,[AS_HELP_STRING([--disable-tcpcache],[Disable distributed cache system])])

CPPCMS_LIBS=""

if test "x$enable_forkcache" != "xno" ; then
	case $host in 
	*cygwin*)	echo "=============================================="
			echo "Cygwin pthread process shared mutex is broken "
			echo "Shared memory/fork cache will be disabled " 
			echo "==============================================" ;;
	*)
	AC_CHECK_LIB(mm,main,[
		have_mm=yes
		CPPCMS_LIBS="-lmm $CPPCMS_LIBS"
		AC_DEFINE([EN_FORK_CACHE],[],["Enable fork cache"])
		],
		[	echo "======================================================================"
			echo "OSSP mm library (libmm) not installed" 
			echo "============== The fork cache backend will be disabled ===============" ]) ;;
	esac
fi

if test "x$enable_fastcgi" != "xno" ; then
	AC_CHECK_LIB(fcgi++,main,[
		have_fcgi=yes
		CPPCMS_LIBS="-lfcgi++ $CPPCMS_LIBS"
		AC_DEFINE([EN_FCGI_BACKEND],[],["Enable fastcgi backend"])
		],
		[	echo "======================================================================"
			echo "Fast CGI library not installed"  
			echo "============== FastCGI API will be disabled ==========================" 
			echo "You still have scgi and cgi API" ])
fi

AM_CONDITIONAL(EN_FORK_CACHE,[test "x$have_mm" == "xyes" ])
AM_CONDITIONAL(EN_FCGI_BACKEND,[test "x$have_fcgi" == "xyes" ])

AC_CHECK_HEADER(fastcgi/fcgiapp.h ,[AC_DEFINE([EN_FASTCGI_LONG_PATH],[],["Fastcgi headers in fastcgi dir"])],[])

if test "x$enable_tcpcache" != "xno" ; then

	store_flags="$CXXFLAGS"

	case $host in
		*cygwin*) CXXFLAGS="-D_WIN32_WINNT -D__USE_W32_SOCKETS $CXXFLAGS"
	esac

	AC_CHECK_HEADER([asio.hpp],[have_asio=yes
			need_system=no],[AC_CHECK_HEADER([boost/asio.hpp],[
					have_asio=yes
					AC_DEFINE([USE_BOOST_ASIO],[],["Use boost::asio"])
					need_system=yes],[
					have_asio=no
					need_system=no
					echo "===================================================="
					echo "ASIO library not found... TCP cache will be disabled"
					echo "===================================================="  ])])

	CXXFLAGS="$store_flags"
fi

if test "x$need_system" == "xyes" 
then
	AC_CHECK_LIB(boost_system,main,
		[ LIBS="-lboost_system $LIBS" ],
		[	AC_CHECK_LIB(boost_system-gcc-mt,main,
				[LIBS="-lboost_system-gcc-mt $LIBS"],
				[	have_asio=no 
				echo "boost::asio found however boost::system not found"
				echo "     TCP cache disabled"])])
fi

if test "x$have_asio" == "xyes" 
then
	AC_DEFINE([EN_TCP_CACHE],[],["Enable tcp cache"])
	case $host in 
		*cygwin*) LIBS="-lws2_32 $LIBS"
	esac
fi


AM_CONDITIONAL(EN_TCP_CACHE,[test "x$have_asio" == "xyes" ])

AC_CHECK_LIB(cgicc,main,[CPPCMS_LIBS="-lcgicc $CPPCMS_LIBS"],[echo "cgicc not found" ; exit -1])
AC_CHECK_LIB(pthread,pthread_sigmask,[],[echo "Pthreads library not found" ; exit -1])
AC_CHECK_LIB(boost_regex,main,[
	CPPCMS_LIBS="-lboost_regex $CPPCMS_LIBS"
	],
	[AC_CHECK_LIB(boost_regex-gcc-mt,main,[
		CPPCMS_LIBS="-lboost_regex-gcc-mt $CPPCMS_LIBS"
		],[ echo "boost::regex not found" ; exit -1])])
AC_CHECK_LIB(boost_iostreams,main,[
	CPPCMS_LIBS="-lboost_iostreams $CPPCMS_LIBS"
	],[AC_CHECK_LIB(boost_iostreams-gcc-mt,main,[
		CPPCMS_LIBS="-lboost_iostreams-gcc-mt $CPPCMS_LIBS"
		],[ echo "boost::iostreams not found" ; exit -1])])

AC_SUBST(CPPCMS_LIBS)
AC_OUTPUT
